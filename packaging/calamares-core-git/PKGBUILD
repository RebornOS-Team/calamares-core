# Maintainer: shivanandvp <shivanandvp@rebornos.org>

pkgname=calamares-core-git
pkgver=3.3.8_002
pkgrel=1

arch=('x86_64' 'aarch64')
pkgdesc='Calamares Installer for RebornOS. This is practically just bare Calamares. Please also install calamares-configuration.'
url='https://github.com/RebornOS-Team/calamares-core'
license=('GPL3')
depends=(
    'qt6-base' # For Qt modules
    'python' # For python modules
    'kirigami' # For Qt-Quick (QML) modules
    'kpmcore' # For partitioning

    'mkinitcpio-openswap' # For swap to be used with encryption

    # Temporary stand-in
    'qt6-location'
    'qt6-svg'
    'qt6-tools'
    'qt6-webengine'
    # 'qt5-xmlpatterns' # TODO
    'qt6ct'
    'qqc2-desktop-style'
    'polkit-qt6'
    'kconfig'
    'kcoreaddons'
    'kdbusaddons'
    'ki18n'
    'kpackage'
    'kparts'
    'kpmcore'
    'appstream-qt'
    'libatasmart'
    'libpwquality'
    # 'plasma-framework5' # TODO
    'parted'
    'solid'
    'udisks2'
    'yaml-cpp'
    'polkit'
    'ckbcomp'
    'adobe-source-han-sans-otc-fonts'
    'python'
    'adobe-source-han-serif-otc-fonts'
    'mkinitcpio-openswap'
)
optdepends=(
    'breeze' # For theming
    'qt6ct'  # For theming
)
provides=('calamares')
conflicts=('calamares' 'calamares-core' 'calamares-core-git' 'calamares-core-local')
makedepends=(
    'base-devel'          # For Arch Linux to build packages.
    'git'                 # For Arch Linux to build packages

    'gcc'                 # Required for building Calamares.         
    'cmake'               # Required for building Calamares.
    'yaml-cpp'            # Required for building Calamares.
    'qt6-base'            # Required for building Calamares.
    
    'python'              # Optional for building Calamares, specifically for PyBind11 (which we use as of 2024-02-09), and Python modules (which we use as of 2024-02-09).
    'extra-cmake-modules' # Optional for building Calamares, specifically for building tests.
    'python-yaml'         # Optional for building Calamares, specifically for configvalidator.
    'python-jsonschema'   # Optional for building Calamares, specifically for configvalidator.
    'kpmcore'             # Optional for building Calamares, specifically for building libcalamares. 
    'gettext'             # Optional for building Calamares, specifically for translations.
    'ki18n'               # Optional for building Calamares, specifically for translations. 
    'qt6-declarative'     # Optional for building Calamares, specifically for QML.
    'kwidgetsaddons'      # Optional for building Calamares, specifically for NA.
    'kcoreaddons'         # Optional for building Calamares, specifically for NA.  

    'icu'                 # Required for building the modules users, and usersq.
    'kparts'              # Required for building the module interactiveterminal. 
    'kservice'            # Required for building the module interactiveterminal. 
    'kpmcore'             # Required for building the modules partition, fsresizer.
    # 'plasma-framework5'   # TODO # Required for building the module plasmalnf.
    'kpackage'            # Required for building the module plasmalnf.

    'ki18n'               # Optional for building the module users.
    'kconfig'             # Optional for building the module plasmalnf, plymouthcfg.
    # 'plasma-framework5'   # TODO # Optional for building the module plymouthcfg.
    'kpackage'            # Optional for building the module plymouthcfg.
    'libxcrypt'           # Optional for building the module users.
    'pkgconf'             # Optional for building the module users.
    'libpwquality'        # Optional for building the module users.
    'parted'              # Optional for building the module welcome.
)

source=("git+https://github.com/RebornOS-Team/calamares-core")  
sha256sums=('SKIP')           

pkgver() {
    (   cd "${srcdir}/calamares-core"
        set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {     
    PROJECT_DIRECTORY="${srcdir}/calamares-core"
    RESOURCE_DIRECTORY="$PROJECT_DIRECTORY/packaging/resources"
    BUILD_DIRECTORY="$PROJECT_DIRECTORY/build"
    NUMBER_OF_PROCESSORS="$(nproc)"
    NUMBER_OF_PROCESSORS="$(($NUMBER_OF_PROCESSORS-1))"
    # TARGET_DIRECTORY_STUB="etc/calamares"
    # DIST_DIRECTORY="$PROJECT_DIRECTORY/$TARGET_DIRECTORY_STUB" 

    mkdir -p "$BUILD_DIRECTORY" && \
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -B "$BUILD_DIRECTORY" -S "$PROJECT_DIRECTORY" && \
    cmake --build "$BUILD_DIRECTORY" --parallel "$NUMBER_OF_PROCESSORS"
}

package() {
    PROJECT_DIRECTORY="${srcdir}/calamares-core"
    RESOURCE_DIRECTORY="$PROJECT_DIRECTORY/packaging/resources"
    BUILD_DIRECTORY="$PROJECT_DIRECTORY/build"
    NUMBER_OF_PROCESSORS="$(nproc)"
    NUMBER_OF_PROCESSORS="$(($NUMBER_OF_PROCESSORS-1))"
    # TARGET_DIRECTORY_STUB="etc/calamares"
    # DIST_DIRECTORY="$PROJECT_DIRECTORY/$TARGET_DIRECTORY_STUB" 

    # To bypass makepkg's dependency check during build-time
    depends+=('calamares-config')

    DESTDIR="${pkgdir}" cmake --install "$BUILD_DIRECTORY" # Install after setting an environment variable

    (cd "$RESOURCE_DIRECTORY" && \
        install -Dm 755 run_calamares.sh "${pkgdir}"/etc/calamares/run_calamares.sh
        ln -s /etc/calamares/run_calamares.sh "${pkgdir}"/usr/bin/run_calamares         
    )
    (cd "$RESOURCE_DIRECTORY" && \
        find . \
            -mindepth 1 \
            -maxdepth 1 \
            -type f \
            -name '*.policy' \
            -exec \
                install -Dm 755 "{}" "${pkgdir}/usr/share/polkit-1/actions/{}" \;
    )
    (cd "$RESOURCE_DIRECTORY" && \
        find . \
            -mindepth 1 \
            -maxdepth 1 \
            -type f \
            -name '*.desktop' \
            -exec \
                install -Dm 755 "{}" "${pkgdir}/usr/share/applications/{}" \;
    )
    (cd "$RESOURCE_DIRECTORY" && \
        find . \
            -mindepth 1 \
            -maxdepth 1 \
            -type f \
            -name '*.svg' \
            -exec \
                install -Dm 755 "{}" "${pkgdir}/usr/share/pixmaps/{}" \;
    )
}
